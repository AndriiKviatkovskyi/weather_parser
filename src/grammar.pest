integer = @{ "-"? ~ ASCII_DIGIT+}
float = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
time = @{ ASCII_DIGIT{2} ~ ":" ~ ASCII_DIGIT{2} }

string = @{ (!";" ~ ANY)+ }
string_collection = { string ~ (";" ~ string)* }

condition = _{ "Condition:" ~ " "? ~ condition_type }
condition_type = @{
    "Clear" | "Partly Cloudy" | "Cloudy" | "Overcast" | "Fog" | "Thunderstorm" | "Rain" | "Drizzle" | "Snow" | "Sleet"
}

temperature = _{ "Temperature:" ~ " "? ~ (float | integer) ~ "C" }

humidity = _{ "Humidity:" ~ " "? ~ (float | integer) ~ "%" }

wind = ${ "Wind:" ~ " "? ~ direction  ~ " "? ~ (float | integer) ~ "km/h" }
direction = @{ "NE" | "SE" | "SW" |  "NW" | "E" | "W" | "N" | "S" }

precipitation = ${ "Precipitation:" ~ " "? ~ precipitation_type ~ " "? ~ (float | integer) ~ "mm"}
precipitation_type = @{ "Rain" | "Snow" | "Sleet" | "Hail" }

visibility = _{ "Visibility:" ~ " "? ~ (float | integer) ~ "km" }

cloud_cover = _{ "Cloud Cover:" ~ " "? ~(float | integer) ~ "%" }

cloud_types = ${ "Cloud Types:" ~ " " ~ cloud_type ~ (", " ~ cloud_type)* }
cloud_type = @{ "Cumulus" | "Stratus" | "Cirrus" | "Cumulonimbus" | "Altostratus" | "Nimbostratus" | "Altocumulus" | "Stratocumulus" }

pressure = _{ "Pressure:" ~ " "? ~ integer ~ "hPa" }

uv_index = _{ "UV Index:" ~ " "? ~ integer }

air_quality = _{ "Air Quality:" ~ " "? ~ air_quality_level }
air_quality_level = @{
    "Good" | "Moderate" | "Unhealthy for Sensitive Groups" | "Unhealthy" | "Very Unhealthy" | "Hazardous"
}

sunrise = _{ "Sunrise:" ~ " "? ~ time }
sunset = _{ "Sunset:" ~ " "? ~ time }